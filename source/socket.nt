import {
  AF,
  core.errno {Errno, report-system-error, system},
  core.memory {load-int16, store-int},
  core.text {_from-c-string, _get-content-pointer, format, text-byte-length},
  core.word,
  this.address-family {address-family},
  this.socket-type {SOCK_STREAM, socket-type},
}

foreign {
  socket(int, int, int): int,
  setsockopt(int, int, int, pointer, int): int,
  getsockopt(int, int, int, pointer, pointer): int,
  inet_addr(pointer): int,
  inet_ntop(int, pointer, pointer, int): pointer,
  bind(int, pointer, int): int,
  listen(int, int): int,
  accept(int, pointer, pointer): int,
  htons(int16): int16,
  ntohs(int16): int16,
  close(int): int,
  snprintf(pointer, int, pointer): int,
  sprintf(pointer, pointer): int,
}

constant sockaddr-in-size: int {
  16
}

constant SOL_SOCKET: int {
  introspect os {
  | darwin =>
    65535
  | linux =>
    1
  }
}

constant SO_REUSEADDR: int {
  introspect os {
  | darwin =>
    4
  | linux =>
    2
  }
}

constant INET_ADDRSTRLEN: int {
  16
}

define _make-socket-reusable(s: descriptor): system(unit) {
  let option-size: int = core.word.size in
  let enable = magic alloca(int, 1) in
  store-int(1, enable);
  let result: int = magic external setsockopt(s, SOL_SOCKET, SO_REUSEADDR, enable, option-size) in
  let so-reuse-port: int = 512 in
  let _: int = magic external setsockopt(s, SOL_SOCKET, so-reuse-port, enable, option-size) in
  if eq-int(result, 0) {
    Right(Unit)
  } else {
    report-system-error()
  }
}

define _create-socket(af: address-family, comm-type: socket-type, protocol: int): system(descriptor) {
  let d =
    magic external socket(
      AF.decode(af),
      this.socket-type.decode(comm-type),
      protocol,
    )
  in
  if lt-int(d, 0) {
    report-system-error()
  } else {
    Right(magic cast(int, descriptor, d))
  }
}

define _inet-addr(address: &text): int {
  let addr'' = core.text.append(address, "\0") in
  let result2 on addr'' = magic external inet_addr(_get-content-pointer(addr'')) in
  let _ = addr'' in
  result2
}

define _bind-socket(af: address-family, s: descriptor, address: &text, port: int16): system(unit) {
  introspect os {
  | darwin =>
    match af {
    | AF.AF_INET =>
      let server-addr = magic alloca(int8, 16) in
      let port': int16 = magic external htons(port) in
      let in-addr = _inet-addr(address) in
      magic store(int8, sockaddr-in-size, server-addr);
      magic store(int8, AF.decode(af), add-int(server-addr, 1));
      magic store(int16, port', add-int(server-addr, 2));
      magic store(int32, in-addr, add-int(server-addr, 4));
      let result = magic external bind(s, server-addr, sockaddr-in-size) in
      if lt-int(result, 0) {
        report-system-error()
      } else {
        Right(Unit)
      }
    }
  | linux =>
    match af {
    | AF.AF_INET =>
      let server-addr = magic alloca(int8, 16) in
      let port': int16 = magic external htons(port) in
      let in-addr = _inet-addr(address) in
      magic store(int16, AF.decode(af), add-int(server-addr, 0));
      magic store(int16, port', add-int(server-addr, 2));
      magic store(int32, in-addr, add-int(server-addr, 4));
      let result = magic external bind(s, server-addr, sockaddr-in-size) in
      if lt-int(result, 0) {
        report-system-error()
      } else {
        Right(Unit)
      }
    }
  }
}

define _listen-socket(s: descriptor, backlog: int): system(unit) {
  let result = magic external listen(s, backlog) in
  if lt-int(result, 0) {
    report-system-error()
  } else {
    Right(Unit)
  }
}

data socket-address {
| Socket-Address(
    ip-address: text,
    port: int16,
  )
}

data client-summary {
| Client-Summary(
    fd: descriptor,
    address: socket-address,
  )
}

inline _consumer<a>(ch: &channel(client-summary), request-interpreter: (socket-address, text) -> text): system(a) {
  let f =
    define loop(): system(a) {
      let summary = receive(ch) in
      let Client-Summary of {fd, address} = summary in
      try request = read(fd) in
      let response = request-interpreter(address, request) in
      let _ on response = write(response, fd) in
      try _ = close(fd) in
      let _ = response in
      loop()
    }
  in
  f()
}

inline _process-request(ch: &channel(client-summary), s: descriptor): system(unit) {
  let client-addr = magic alloca(int8, 16) in
  let address = magic alloca(int8, 4) in
  magic store(int32, sockaddr-in-size, address);
  let client-fd = magic external accept(s, client-addr, address) in
  let client-len: int = magic load(int32, address) in
  let _ = client-len in
  if lt-int(client-fd, 0) {
    report-system-error()
  } else {
    let client-ip-ptr = magic alloca(int16, 1) in
    magic external inet_ntop(AF.decode(AF.AF_INET), add-int(client-addr, 4), client-ip-ptr, INET_ADDRSTRLEN);
    let client-ip-address = _from-c-string(client-ip-ptr) in
    let client-port = load-int16(add-int(client-addr, 2)) in
    let client-port': int16 = magic external ntohs(client-port) in
    let fd = magic cast(int, descriptor, client-fd) in
    let summary = Client-Summary(fd, Socket-Address(client-ip-address, client-port')) in
    send(ch, summary);
    Right(Unit)
  }
}

define _create-server-socket(
  af: address-family,
  comm-type: socket-type,
  reuse-socket: bool,
  protocol: int,
  address: &text,
  port: int16,
  connection-queue-size: int,
): system(descriptor) {
  try s = _create-socket(af, comm-type, protocol) in
  try _ =
    if reuse-socket {
      _make-socket-reusable(s)
    } else {
      Right(Unit)
    }
  in
  try _ = _bind-socket(af, s, address, port) in
  try _ = _listen-socket(s, connection-queue-size) in
  Right(s)
}

define _consumer-loop<a>(q: &channel(thread(system(unit)))): system(a) {
  let val-thread = receive(q) in
  try _ = attach {val-thread} in
  _consumer-loop(q)
}

define _wait-all<a>(ys: list(thread(a))): unit {
  match ys {
  | Nil =>
    Unit
  | Cons(z, zs) =>
    let _ = attach {z} in
    _wait-all(zs)
  }
}

inline _create-consumers(
  ch: &channel(client-summary),
  n: int,
  f: (socket-address, text) -> text,
): list(thread(system(unit))) {
  let f =
    define self(n: int): list(thread(system(unit))) {
      if eq-int(n, 0) {
        []
      } else {
        let consumer = detach {_consumer(ch, f)} in
        let consumers = self(sub-int(n, 1)) in
        Cons(consumer, consumers)
      }
    }
  in
  f(n)
}

inline _server-loop(num-of-threads: int, s: descriptor, f: (socket-address, text) -> text): system(unit) {
  let ch = new-channel() in
  let result on ch =
    let consumers = _create-consumers(ch, num-of-threads, f) in
    let producer =
      define self(): system(unit) {
        try _ = _process-request(ch, s) in
        self()
      }
    in
    try _ = producer() in
    // unreachable
    Right(_wait-all(consumers))
  in
  let _ = ch in
  result
}

data config {
| Config(
    family: address-family,
    comm-type: socket-type,
    reuse-socket: bool,
    protocol: int,
    port: int16,
    address: &text,
    backlog: int,
    threads: int,
    interpreter: (socket-address, text) -> text,
  )
}

inline start-server(c: config): system(unit) {
  let Config of {family, comm-type, reuse-socket, protocol, address, port, backlog, interpreter, threads} = c in
  try s = _create-server-socket(family, comm-type, reuse-socket, protocol, address, port, backlog) in
  printf("listening {}:{}\n", [*address, %ld(magic cast(_, _, port))]);
  _server-loop(threads, s, interpreter)
}

define zen(): unit {
  let server-config =
    Config of {
      family = AF.AF_INET,
      comm-type = SOCK_STREAM,
      reuse-socket = True,
      protocol = 0,
      port = 8080,
      address = "0.0.0.0",
      backlog = 128,
      threads = 10,
      interpreter = {
        function (address: socket-address, t: text): text {
          let Socket-Address of {ip-address, port} = address in
          printf("client: {}:{}\n", [ip-address, %ld(magic cast(_, _, port))]);
          let len on t = text-byte-length(t) in
          let body = format("{\"key\": \"Hello, world!\", \"request-length\": {}}", [%ld(len)]) in
          let body-len on body = text-byte-length(body) in
          printf("request:\n{}\n", [t]);
          format("HTTP/1.1 200 OK\r\nContent-Length: {}\r\n\r\n{}", [%ld(body-len), body])
        }
      },
    }
  in
  match start-server(server-config) {
  | Left(Errno(i)) =>
    printf("failed to start a server. error code: {}\n", [%ld(i)])
  | Right(_) =>
    Unit
  }
}

define main(): unit {
  zen()
}
